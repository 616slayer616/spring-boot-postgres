buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.asciidoctor.convert'


group 'de.adler.springboot_postgres'
version '1.0'

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

jar {
    baseName = 'springboot_postgres'
    version = '1.0'
    //noinspection GroovyAssignabilityCheck
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

asciidoctor {
    //noinspection GroovyAssignabilityCheck
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    //noinspection GroovyAssignabilityCheck
    dependsOn test
}

repositories {
    mavenCentral()
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.7
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.7

dependencies {
    // tag::jetty[]
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    // end::jetty[]
    // tag::actuator[]
    compile("org.springframework.boot:spring-boot-starter-actuator")
    // end::actuator[]

    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    compile group: 'org.postgresql', name: 'postgresql', version: '42.0.0'

    testCompile group: 'com.h2database', name: 'h2', version: '1.4.194'
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.2.RELEASE'
    testCompile("junit:junit")
}